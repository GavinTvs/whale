// Top-level build file where you can add configuration options common to all sub-projects/modules.
project.ext {
    mApplicationId = "com.gavin.whale"

    appName = "52Hz鲸"

    isTabIRun = false
    isTabIIRun = false
    isTabIIIRun = false

    compileSdkVer = 28
    buildToolsVer = "28.0.3"
    targetSdkVer = 26
    minSdkVer = 16

    /**
     * 通用设置
     */
    setDefaultConfig = {
        extension ->
            //通用脚本引入
            extension.apply plugin: 'com.jakewharton.butterknife'
            extension.apply plugin: 'kotlin-android'
            extension.apply plugin: 'kotlin-android-extensions'
            extension.apply plugin: 'kotlin-kapt'

            setAndroidConfig extension.android
            setDependencies extension.dependencies

            //优化编译时间
            extension.android.dexOptions {
                preDexLibraries true
                maxProcessCount 8
            }
    }

    //设置App配置
    setAppDefaultConfig = {
        extension ->
            extension.apply plugin: 'com.android.application'
            extension.description "app"
            setDefaultConfig extension

            extension.android.defaultConfig {
                if ("app" != extension.getName()) {
                    applicationId mApplicationId + "." + extension.getName()
                } else {
                    applicationId mApplicationId

                }
            }
            /**
             * 模块化编译时根据模块名动态更改属性值
             * 更改applicationId和app_name
             */
            extension.android.buildTypes {
                if ("app" != extension.getName()) {
                    appName = extension.getName()
                }
                debug {
                    //applicationIdSuffix ".debug"
                    resValue("string", "app_name", appName + "_DEBUG")
                }
                release {
                    resValue("string", "app_name", appName)
                }
            }
    }

    //设置Lib配置
    setLibDefaultConfig = {
        extension ->
            extension.apply plugin: 'com.android.library'
            extension.description "lib"
            setDefaultConfig extension

            extension.android {
                defaultConfig {
                    //Library 如果不指定defaultPublishConfig 构建都会是release版本
                    defaultPublishConfig 'debug'

                    //指定组件混淆规则合并目录
                    consumerProguardFiles 'proguaed-rules.pro'
                }
            }
            setDependencies extension.dependencies
    }



    //设置Android配置
    setAndroidConfig = {
        extension ->
            extension.compileSdkVersion compileSdkVer
            extension.buildToolsVersion buildToolsVer

            extension.defaultConfig {
                targetSdkVersion targetSdkVer
                minSdkVersion minSdkVer

                //Java 1.8支持
                extension.compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                //ARouter 初始化配置
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments = [AROUTER_MODULE_NAME: extension.project.getName()]
                    }
                }
            }

            extension.buildTypes {
                debug {
                    extension.defaultConfig { minSdkVersion 16 }
                }
                release {
                    extension.defaultConfig { minSdkVersion 16 }
                }
            }

            extension.dataBinding {
                enabled = true
            }
            extension.project.tasks.whenTaskAdded { task ->
                if (task.name.contains("lint")//不启动lint代码扫描潜在bug可以使用
//                        || task.name.equals("clean")
                        || task.name.contains("Aidl") //项目中有用到aidl则不可以舍弃这个任务
                        || task.name.contains("mockableAndroidJar")//用不到测试的时候就可以先关闭
                        || task.name.contains("UnitTest")//用不到测试的时候就可以先关闭
                        || task.name.contains("AndroidTest")//用不到NDK和JNI的也关闭掉
                        || task.name.contains("Ndk")
                        || task.name.contains("Jni")
                ) {
                    task.enabled = false
                }
            }
    }

    //设置依赖
    setDependencies = {
        extension ->
            extension.api fileTree(dir: 'libs', include: ['*.jar'])
    }
}